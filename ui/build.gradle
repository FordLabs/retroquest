plugins {
    id 'com.github.node-gradle.node' version '3.1.0'
    id 'org.sonarqube'
}

node {
    download = true
    version = "12.16.2"
}

task check(type: YarnTask) {
    args = ['unit-coverage']

    inputs.files('package.json', 'yarn.lock', 'tsconfig.json')
    inputs.dir('src')
    inputs.dir(fileTree('node_modules').exclude('.cache'))

    outputs.files('test-report.xml')
    outputs.dir('coverage')
}

task build(type: YarnTask) {
    args = ['build']

    inputs.files('package.json', 'yarn.lock', 'tsconfig.json')
    inputs.dir('src')
    inputs.dir(fileTree('node_modules').exclude('.cache'))

    outputs.dir('build')
}

build.configure {
    dependsOn yarn_install
}

sonarqube {
    properties {
        property "sonar.projectName", "RetroQuest UI"
        property "sonar.projectKey", "retroquest_ui"
        property "sonar.organization", "fordlabs"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.sources", "./src/app"
        property "sonar.exclusions", "**/*.spec.ts, src/testing/*, e2e/*"
        property "sonar.tests", "./src/app"
        property "sonar.test.inclusions", "**/*.spec.ts, src/testing/*, e2e/*"

        property "sonar.testExecutionReportPaths", "test-report.xml"
        property "sonar.javascript.lcov.reportPaths", "./coverage/lcov.info"
    }
}
